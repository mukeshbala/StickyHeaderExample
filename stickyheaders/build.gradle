apply plugin: 'com.android.library'

group = 'org.zakariya.stickyheaders'
version = '0.7.5'

android {
	compileSdkVersion 23
	buildToolsVersion '25.0.0'

	defaultConfig {
		minSdkVersion 11
		targetSdkVersion 23
		versionCode 1
		versionName version
	}
	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
	}
}

dependencies {
	compile fileTree(dir: 'libs', include: ['*.jar'])
	testCompile 'junit:junit:4.12'
	compile 'com.android.support:recyclerview-v7:23.4.0'
}

task generateSourcesJar(type: Jar) {
	from android.sourceSets.main.java.srcDirs
	classifier = 'sources'
}

task generateJavadocs(type: Javadoc) {
	source = android.sourceSets.main.java.srcDirs
	classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
	failOnError false // TODO: Get recyclerview into classpath for javadoc to work correctly
}

task generateJavadocsJar(type: Jar, dependsOn: generateJavadocs) {
	from generateJavadocs.destinationDir
	classifier 'javadoc'
}

artifacts {
	archives generateJavadocsJar
	archives generateSourcesJar
}